buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

crashlytics {
    enableNdk = true;
}

android {
    compileSdkVersion = 27
    buildToolsVersion = '27.0.3'

    dataBinding {
        enabled = true
    }

    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner = 'android.support.test.runner.AndroidJUnitRunner'
        applicationId = 'io.digibyte'
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 2343
        versionName "2.0.3b2343"
        multiDexEnabled true
        // Similar to other properties in the defaultConfig block,
        // you can configure the ndk block for each product flavor
        // in your build configuration.
        ndk {
            // Specifies the ABI configurations of your native
            // libraries Gradle should build and package with your APK.
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_TOOLCHAIN=clang"
            }
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        pickFirst 'protobuf.meta'
    }

    externalNativeBuild {
        cmake {
            // When you specify a version of CMake, as shown below,
            // the Android plugin searches for its binary within your
            // PATH environmental variable.
            path "CMakeLists.txt"  //path can only be set outside (in android block)

        }
    }

    // Specifies two flavor dimensions.
    flavorDimensions "mode"

    productFlavors {
        digibyte {
            applicationId "io.digibyte"
            dimension "mode"
            resValue "string", "app_name", "DigiByte"
            buildConfigField "boolean", "BITCOIN_TESTNET", "false"

            externalNativeBuild {
                cmake {
                    // When you specify a version of CMake, as shown below,
                    // the Android plugin searches for its binary within your
                    // PATH environmental variable.
                    cFlags "-DBITCOIN_TESTNET=0"
                    targets "core-lib"
                }
            }
        }

        digibyteTestnet {
            applicationId "io.digibytetestnet"
            dimension "mode"
            resValue "string", "app_name", "digibyte-testnet"
            buildConfigField "boolean", "BITCOIN_TESTNET", "true"

            externalNativeBuild {
                cmake {
                    // When you specify a version of CMake, as shown below,
                    // the Android plugin searches for its binary within your
                    // PATH environmental variable.
                    cFlags "-DBITCOIN_TESTNET=1"
                    targets "core-lib"
                }
            }

        }

    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet false
        explainIssues true
        // if true, stop the gradle build if errors are found
        abortOnError true
        // if true, only report errors
        ignoreWarnings false

        checkReleaseBuilds false

        disable 'MissingTranslation'
    }

    buildTypes {
        gitUpdateModules()
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled = true
            debuggable = false

            ndk {
                minifyEnabled = false
                debuggable = false
            }
        }
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable = true
            minifyEnabled = true

            ndk {
                minifyEnabled = false
                debuggable = true
            }
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

task copyResDirectoryToClasses(type: Copy) {
    from "${projectDir}/src/test/res"
    into "${buildDir}/intermediates/classes/test/debug/res"
}

gradle.projectsEvaluated {
    assembleDebug.dependsOn(copyResDirectoryToClasses)
}


def gitUpdateModules() {
    //['sh', '-c', 'git submodule foreach git pull origin master'].execute().text.trim()
    getLogger().debug("gitUpdateModules done!")
}

repositories {
    jcenter()
    mavenCentral()
    google()
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'commons-io:commons-io:2.5'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-vector-drawable:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:gridlayout-v7:27.1.1'
    implementation 'com.android.support:support-v13:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'io.sigpipe:jbsdiff:1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support.test:runner:1.0.2'
    implementation 'com.android.support.test:rules:1.0.2'
    implementation 'junit:junit:4.12'
    implementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'jp.wasabeef:recyclerview-animators:2.3.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.2@aar') {
        transitive = true;
    }
    def room_version = "1.1.1"
    implementation "android.arch.persistence.room:runtime:$room_version"
    annotationProcessor "android.arch.persistence.room:compiler:$room_version"
    implementation 'com.facebook.conceal:conceal:2.0.2@aar'
    implementation 'com.tarun0.zxing-standalone:zxing-standalone:1.0.0'
    implementation 'com.evernote:android-job:1.2.6'
    implementation 'com.airbnb.android:lottie:2.5.4'
    implementation 'com.appolica:flubber:1.0.1'
    implementation 'com.ToxicBakery.viewpager.transforms:view-pager-transforms:1.2.32@aar'
    implementation 'com.github.Binary-Finery:Bungee:master-SNAPSHOT'
    implementation 'com.scottyab:rootbeer-lib:0.0.6'
    implementation 'com.google.guava:guava:24.1-android'
}
